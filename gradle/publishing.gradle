/*
 * Copyright 2016 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

// configuration of the Maven artifacts
apply plugin: 'maven-publish'

// custom tasks for creating source/javadoc jars
task sourcesJar(type: Jar, dependsOn: classes) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

// add javadoc/source jar tasks as artifacts
artifacts {
  archives sourcesJar, javadocJar
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      from components.java

      artifact sourcesJar {
        classifier "sources"
      }

      artifact javadocJar {
        classifier "javadoc"
      }

      pom.withXml {
        def Node root = asNode()
        root.appendNode('name', 'Gradle Plugin for Groovy on Android')
        root.appendNode('description', 'Adds support for the Groovy language to Android builds')
        root.appendNode('url', 'https://github.com/groovy/groovy-android-gradle-plugin')

        def issues = root.appendNode( 'issueManagement' )
        issues.appendNode('system', 'github' )
        issues.appendNode('url', 'https://github.com/groovy/groovy-android-gradle-plugin/issues')

        def scm = root.appendNode( 'scm' )
        scm.appendNode( 'url', 'https://github.com/groovy/groovy-android-gradle-plugin')
        scm.appendNode( 'connection', 'scm:git:https://github.com/groovy/groovy-android-gradle-plugin.git')
        scm.appendNode( 'developerConnection', 'scm:git:git@github.com/groovy/groovy-android-gradle-plugin.git')

        def license = root.appendNode('licenses').appendNode('license')
        license.appendNode( 'name', 'The Apache Software License, Version 2.0' )
        license.appendNode( 'url', 'http://www.apache.org/licenses/LICENSE-2.0.txt' )
        license.appendNode( 'distribution', 'repo' )

        def dev = root.appendNode('developers').appendNode('developer')
        dev.appendNode('id', 'groovy')
        dev.appendNode('name', 'Cedric Champeau')
        dev.appendNode('organization', 'Groovy' )
        dev.appendNode('organizationUrl', 'http://www.groovy-lang.org' )
      }
    }
  }
}
